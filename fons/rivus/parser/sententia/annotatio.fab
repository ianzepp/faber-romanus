# Annotation Parsing Helpers
#
# Parses line-based annotations like:
#   @ futura
#   @ radix imperativus, perfectum
#
# Morphology annotations only accept identifiers on the same line.

ex "../resolvitor" importa Resolvitor
ex "../../ast/positio" importa Locus
ex "../../ast/lexema" importa SymbolumGenus
ex "../errores" importa ParserErrorCodice

# Parsed annotation
@ publicum
genus Annotatio {
    Locus locus
    textus nomen
    lista<textus> argumenta
}

functio estFormaRadix(textus forma) -> bivalens {
    redde forma inter ["imperativus", "perfectum", "futurum_indicativum", "futurum_activum"]
}

@ publica
functio parseAnnotationes(Resolvitor r) -> lista<Annotatio> {
    fixum p = r.parser()
    varia annotationes = [] qua lista<Annotatio>

    dum p.congruet(SymbolumGenus.At) {
        fixum locus = p.praevius().locus
        fixum linea = locus.linea
        fixum sym = p.specta(0)

        varia nomen = ""
        si sym.species == SymbolumGenus.Nomen aut sym.species == SymbolumGenus.Verbum {
            p.procede()
            nomen = sym.valor
        } secus {
            p.renuncia(ParserErrorCodice.ExpectaturNomen, scriptum("got 'ยง'", sym.valor))
            p.procede()
        }

        varia argumenta = [] qua lista<textus>

        si nomen == "radix" {
            dum non p.estFinis() {
                fixum next = p.specta(0)
                si next.locus.linea != linea {
                    rumpe
                }

                si next.species == SymbolumGenus.Coma {
                    p.procede()
                    perge
                }

                si next.species == SymbolumGenus.Nomen aut next.species == SymbolumGenus.Verbum {
                    p.procede()
                    si estFormaRadix(next.valor) {
                        argumenta.adde(next.valor)
                    } secus {
                        p.renuncia(ParserErrorCodice.InvalidaRadixForma, scriptum("got 'ยง'", next.valor))
                    }
                    perge
                }

                p.renuncia(ParserErrorCodice.InvalidaRadixForma, scriptum("got 'ยง'", next.valor))
                p.procede()
            }
        }

        annotationes.adde({
            locus: locus,
            nomen: nomen,
            argumenta: argumenta
        } qua Annotatio)
    }

    redde annotationes
}
